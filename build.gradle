plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.jaredsburrows.license' version '0.9.3'
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

group = 'dev.fwcd.mcdiscordbridge'
version = '0.3.3'

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven {
        name = 'spigot-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven { url 'https://repo.mikeprimm.com' }
}

dependencies {
    implementation 'net.dv8tion:JDA:5.0.0-beta.15'
    implementation 'club.minnced:discord-webhooks:0.8.4'
    implementation 'commons-io:commons-io:2.14.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    compileOnly 'us.dynmap:dynmap-api:3.6'
    compileOnly 'org.spigotmc:spigot-api:1.20.2-R0.1-SNAPSHOT'
}

jar {
    enabled = false
}

shadowJar {
    archiveBaseName.set('DiscordBridge')

    // Unfortunately, we cannot shade the logging implementation since SLF4J
    // (which JDA uses) otherwise won't find the StaticLoggerBinder and fall
    // back to writing everything to stdot.
    
    // To avoid shading the logging implementation, we have to list every
    // dependency that we wish to include explicitly instead. Upstream support
    // for exclusions is pending: https://github.com/johnrengelman/shadow/pull/555

    def packagesToRelocate = [
        "club.minnced",
        "com.fasterxml",
        "com.google",
        "com.iwebpp",
        "com.neovisionaries",
        "com.sun.jna",
        "org.apache.commons",
        "gnu",
        "kotlin",
        "net.dv8tion",
        "okhttp3",
        "okio",
        "org.intellij",
        "org.jetbrains",
        "org.json",
        "tomp2p"
    ]
    def relocationPrefix = "${project.group}.dependencies"

    for (packageToRelocate : packagesToRelocate) {
        relocate packageToRelocate, "${relocationPrefix}.${packageToRelocate}"
    }
}
